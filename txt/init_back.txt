###
### Logo
###

@bg white.png 0
@ch c suika.png 1.0
@se suika.ogg
@wait 2.0
@ch c none 1.0

###
### Menu
###

@vol bgm 1.0
@bgm 00.ogg
@bg menu.png 1.0

:MENU
@menu menu.png menu_selected.png START 2 495 314 192 LOAD 316 494 322 203 CONFIG 640 492 317 204 QUIT 960 497 317 201

:START
@se click.ogg
@goto SCENE1

:LOAD
@se click.ogg
@bg menu.png
@bg load-bg.png 1.0
@goto $LOAD
@bg menu.png 1.0
@goto MENU

:CONFIG
@se click.ogg
@bg menu.png
@bg config.png 1.0
@goto CONFIG_SCREEN

:QUIT
@se click.ogg
@goto END

###
### Scene 1 (Mixture of Midori's lines and monologues)
###

:SCENE1

@vol bgm 0.0 1.0
@bg white.png 1.0
@bgm cicada.ogg
@vol bgm 1.0 1.0
@bg roof.png 1.0
@ch c 001-fun.png 1.0
中文测试
*???*Oh, what a nice breeze...

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(I'm on the roof of my school, having skipped the remedial class.)

@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*???*Hey, it's fine, they don't call the roll.

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(This is a country town in Hokkaido. I can just see the coastline on the horizon, since there aren't any buildings near the school.)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*???*Are you asking me why I skipped the remedial class?

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(Oh - I forgot to mention, I'm Midori. I'm a high school student in this country town. My parents are watermelon farmers.)

@vol bgm 1.0 1.0
@ch c 001-angry.png 1.0
*Midori*It's because...

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(Oh, I have to tell you something important. The reason why I'm here today is...)

@vol bgm 1.0 1.0
@ch c 001-normal.png 1.0
*Midori*I want to...

@vol bgm 0.0 1.0
@ch c 001-happy.png 1.0
*Midori*I want to tell you about Suika2!

###
### Scene2 (Greetings on the roof)
###

@bgm 02.ogg
@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*Midori*Hello! Thank you for using Suika2!
*Midori*As you might know, Suika2 is a visual novel authoring tool.
*Midori*It's free and runs on both Windows and Mac. Awesome, right?

@ch c 001-fun.png 1.0
*Midori*The author says he wants you to ask questions. Do you have any questions for him?
@ch c none 1.0

###
### Scene3 (Be asked questions on the roof)
###

:LOOP1
@select L1_ADVANTAGE L1_ENV L1_NEXT "Why choose Suika2?" "What are the system requirements?" Next

:L1_ADVANTAGE
Suika2 has three advantages. First of all, portability. We have Windows and Mac apps and a Web browser version. All of them work just the same way.
Game developers don't need to bother about the users' OSes.
In addition, game developers can use their favorite OS to develop a game.
Secondly, simplicity. Anyone can start writing scripts easily.
Last but not least, freedom. You can use Suika2 commercially for free.

@set $1 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, wow! I didn't know that. Very cool.
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP1

:L1_ENV
For Windows, we support Windows 8.1 and later. We also support the latest Windows 10 tablets.
For Mac, we support macOS 10.9 and later. We certainly support the latest Big Sur release and M1 chip.
For Web browsers, we support Chrome, Safari, Firefox and Edge.
In addition, Suika2 runs on iPhone, iPad, Android and Linux. Actually, Suika2 was initially written on Linux!
@set $2 = 1
@ch c 001-fun.png 1.0
*Midori*Yes! That's right.
*Midori*The author says he wants you to ask some more questions.
@ch c none 1.0
@goto LOOP1

:L1_NEXT

###
### Scene4 (Be asked questions on the coast)
###

@ch c 001-fun.png 1.0
*Midori*Shall we step out and have a walk? Where should we go? ...Well, how about the coast?

@vol bgm 0.0 1.0
@bg coast.png 1.0 c
@vol bgm 1.0 0.0
@bgm coast.ogg
@ch c 001-happy.png 1.0
*Midori*Do you have any further questions?
@ch c none 1.0

:LOOP2
@select L2_OLD L2_CUSTOMIZE L2_NEXT "Do you have Suika1?" "Can I customize it?" Next

:L2_OLD
Versions before 2.0.0 was named Suika Studio. The author developed it when he was just a high school student.
At 2.0.0, he removed the visual editor and "Studio" naming.
@set $3 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, I see. I didn't know that.
*Midori*The author says he wants you to ask more questions.
@ch c none 1.0
@goto LOOP2

:L2_CUSTOMIZE
You can customize the engine with relatively little effort. This includes the font, text box and save screen.
If there's anything you want to customize but can't, please contact the author.
@set $4 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, yes!
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP2

:L2_NEXT

###
### Scene5
### (Last scene on the coast/Mixture of Midori's lines and monologues)
###

@ch c 001-fun.png 1.0
*Midori*Thanks for asking so many questions today!

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(I just wanted to tell you about Suika2...)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*Midori*If you have any questions, send me an email at midori@luxion.jp anytime.

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(Don't hesitate to send me an email. I'll be waiting for your comments.)

@vol bgm 0.0 1.0
@ch c 001-fun.png 1.0
*Midori*See you again!

###
### Scene6 (Added bonus)
###

@if $1 != 1 END
@if $2 != 1 END
@if $3 != 1 END
@if $4 != 1 END
Thank you for selecting all the options.
@goto END

###
### Config Screen
###

:CONFIG_SCREEN
@menu config.png config_selected.png BGM_STOP 468 170 149 62 VOICE_STOP 468 256 149 62 SE_STOP 468 340 149 62 BGM_MEDIUM 655 170 237 62 VOICE_MEDIUM 655 256 237 62 SE_MEDIUM 655 340 237 62 BGM_MAX 933 170 127 62 VOICE_MAX 933 256 127 62 SE_MAX 933 340 127 62 CONFIG_OK 524 598 219 73

:BGM_STOP
@vol BGM 0
@goto CONFIG_SCREEN

:VOICE_STOP
@vol VOICE 0
@goto CONFIG_SCREEN

:SE_STOP
@vol SE 0
@goto CONFIG_SCREEN

:BGM_MEDIUM
@vol BGM 0.5
@goto CONFIG_SCREEN

:VOICE_MEDIUM
@vol VOICE 0.5
@se 025.ogg voice
@goto CONFIG_SCREEN

:SE_MEDIUM
@vol SE 0.5
@se click.ogg
@goto CONFIG_SCREEN

:BGM_MAX
@vol BGM 1.0
@goto CONFIG_SCREEN

:VOICE_MAX
@vol VOICE 1.0
@se 025.ogg voice
@goto CONFIG_SCREEN

:SE_MAX
@vol SE 1.0
@se click.ogg
@goto CONFIG_SCREEN

:CONFIG_OK
@se click.ogg
@bg config.png
@bg menu.png 1.0
@goto MENU

###
### Quit Logo
###

:END
@vol bgm 0.0 2.0
@bg white.png 1.0
@ch c luxion.png 0.5
@wait 1.0
@ch c none 1.0
